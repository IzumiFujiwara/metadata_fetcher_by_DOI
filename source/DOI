let
    // テーブルDOIからDOIを取得
    ソース = Excel.CurrentWorkbook(){[Name="DOI"]}[Content],
    変更された型 = Table.TransformColumnTypes(ソース,{{"DOI", type text}}),
    // カスタム関数 getUnpaywallにDOIを渡してデータを取得
    呼び出されたカスタム関数 = Table.AddColumn(変更された型, "getUnpaywall", each getUnpaywall([DOI])),
    // エラーはnullに置換
    置換されたエラー = Table.ReplaceErrorValues(呼び出されたカスタム関数, {{"getUnpaywall", null}}),
    // データをテーブルに展開
    #"展開された getUnpaywall" = Table.ExpandTableColumn(置換されたエラー, "getUnpaywall", 
        {
            "DOI", "issn", "journal_name", "article title","genre", "published_date", "is_oa", "oa_status", "oa_location.license","oa_location.url",  "oa_location.url_for_pdf"
        }, 
        {
         "Unpaywall.DOI", "Unpaywall.issn", "Unpaywall.journal_name", "Unpaywall.article title", 
         "Unpaywall.genre", "Unpaywall.published_date","Unpaywall.is_oa", "Unpaywall.oa_status", 
         "Unpaywall.oa_location.license","Unpaywall.oa_location.url",  "Unpaywall.oa_location.url_for_pdf"
         }),
    変更された型3 = Table.TransformColumnTypes(#"展開された getUnpaywall",{{"Unpaywall.published_date", type date}}),
    //カスタム関数 getSherpaにISSNを渡してデータ（URL)を取得、列uriとして追加する
    呼び出されたカスタム関数1 = Table.AddColumn(変更された型3, "uri", each getSherpa([Unpaywall.issn])),
    // エラーは N/A に置換
    置換されたエラー2 = Table.ReplaceErrorValues(呼び出されたカスタム関数1, {{"uri", "N/A"}}),
    //列を並び替えて uri を2列目に
    並べ替えられた列 = Table.ReorderColumns(置換されたエラー2,
        {
         "DOI", "uri", "Unpaywall.DOI", "Unpaywall.issn", "Unpaywall.journal_name", "Unpaywall.article title", 
         "Unpaywall.genre", "Unpaywall.published_date",
         "Unpaywall.is_oa", "Unpaywall.oa_status", "Unpaywall.oa_location.license", "Unpaywall.oa_location.url", 
         "Unpaywall.oa_location.url_for_pdf"
         }),
    // 型を変換
    変更された型1 = Table.TransformColumnTypes(並べ替えられた列,
    {
        {"uri", type text}, {"Unpaywall.DOI", type text}, {"Unpaywall.issn", type text}, {"Unpaywall.journal_name", type text},
        {"Unpaywall.article title", type text}, {"Unpaywall.genre", type text}, {"Unpaywall.published_date", type date},
        {"Unpaywall.is_oa", type logical}, {"Unpaywall.oa_status", type text},
        {"Unpaywall.oa_location.license", type text}, {"Unpaywall.oa_location.url", type text},
        {"Unpaywall.oa_location.url_for_pdf", type text}
    }),
    // 列DOIを削除
    削除された列 = Table.RemoveColumns(変更された型1,{"Unpaywall.DOI"}),
    置き換えられた値 = Table.ReplaceValue(削除された列,null,"#N/A",Replacer.ReplaceValue,{"Unpaywall.issn"}),
    // SCPJのデータを取り込み
    呼び出されたカスタム関数2 = Table.AddColumn(置き換えられた値, "SCPJ data", each getSCPJdata([Unpaywall.issn])),
    #"展開された SCPJ data" = Table.ExpandTableColumn(呼び出されたカスタム関数2, "SCPJ data", 
        {"ポリシー", "Title", "出版社版の利用", "公開場所", "公開条件", "備考"},
        {"ポリシー", "Title", "出版社版の利用", "公開場所", "公開条件", "備考"}
        ),
    変更された型2 = Table.TransformColumnTypes(#"展開された SCPJ data",{
        {"ポリシー", type text}, {"Title", type text}, {"出版社版の利用", type text}, 
        {"公開場所", type text}, {"公開条件", type text}, {"備考", type any}
        })

in
    変更された型2