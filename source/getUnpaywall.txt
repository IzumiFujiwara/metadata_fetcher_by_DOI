(DOI as text) =>
    let
        unpaywall = Web.Contents("https://api.unpaywall.org/v2", 
            [ RelativePath = DOI , ApiKeyName = "email" , ManualStatusHandling={400, 404, 500} ]),
        // ステータスコードのチェック
        status = Value.Metadata(unpaywall)[Response.Status], 
        unpaywallSource = Table.FromColumns({Lines.FromBinary(unpaywall, null, null)}),
        // Transform each line using Json.Document
        #"Transformed Column" = Table.TransformColumns(unpaywallSource, {"Column1", Json.Document}),
        // DOI, ISSN, Journal Name, Article title, is_oa, oa Status, Best oa Locationの値を取り出してテーブルにセット
        #"展開された Column1" = Table.ExpandRecordColumn(
        #"Transformed Column", "Column1", 
        {"doi", "journal_issn_l", "journal_name", "title",         "is_oa", "oa_status", "best_oa_location"},
        {"DOI", "issn",           "journal_name", "article title", "is_oa", "oa_status", "Column1.best_oa_location"}
        ),
        // Column1.best_oa_locationからのoa_location.license,oa_uri,url_for_pdfを取り出してテーブルに追加
        #"unpaywalldata" = Table.ExpandRecordColumn(
            #"展開された Column1", "Column1.best_oa_location", 
            {"license",            "url",              "url_for_pdf"}, 
            {"oa_location.license","oa_location.url",  "oa_location.url_for_pdf"}
            )
    in
        // 404エラーならnullを返す
        if status = 404 then null else 
        #"unpaywalldata"