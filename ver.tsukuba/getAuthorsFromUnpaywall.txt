= (DOI as text) =>
// UnpaywallのAPIにDOIを送信して返ってくるデータを取り込む関数
let
    // Unpaywall APIからデータを取得
    unpaywall = Web.Contents("https://api.unpaywall.org/v2", 
        [ RelativePath = DOI, ApiKeyName = "email", ManualStatusHandling = {400, 404, 500} ]),
    // ステータスコードのチェック
    status = Value.Metadata(unpaywall)[Response.Status],
    // レスポンスをJSON形式に変換
    jsonResponse = Json.Document(unpaywall),
    // z_authorsフィールドを展開するためにリストを作成
    authorsList = if Record.HasFields(jsonResponse, "z_authors") 
        then Table.FromList(jsonResponse[z_authors], Splitter.SplitByNothing(), {"AuthorRecord"}) 
        else null,
    #"ExpandAuthors" = Table.ExpandRecordColumn(authorsList, "AuthorRecord", {"author_position", "raw_author_name", "is_corresponding", "raw_affiliation_strings"}, {"author_position", "raw_author_name", "is_corresponding", "raw_affiliation_strings"}),
    #"Rename Table" = Table.RenameColumns(
        #"ExpandAuthors", 
        {
            {"author_position", "Author position"},
            {"raw_author_name", "Raw Author Name"},
            {"raw_affiliation_strings", "Raw Affiliation Strings"}
        }
    ),
    #"Expanded Affiliation" = Table.TransformColumns(#"Rename Table", {"Raw Affiliation Strings", each Text.Combine(List.Transform(_, Text.From), "|"), type text}),
    エラーチェック = Table.ReplaceErrorValues(#"Expanded Affiliation", {{"Raw Affiliation Strings", ""}}),
    追加されたインデックス = Table.AddIndexColumn(エラーチェック, "order", 1, 1, Int64.Type),
    #"result" = Table.ReorderColumns(追加されたインデックス,{"order", "Author position", "Raw Author Name", "Raw Affiliation Strings"})
in
    #"result"